100 SCREEN 1:CLS
110 GET DATE DT$
120 C = 20:D = 31
130 Y = VAL(LEFT$(DT$,2)):YK = Y
140 M = VAL(MID$(DT$,4,2)):MK = M
150 IF M = 1 OR M = 2 THEN MK = M + 12:YK = Y - 1
160 IF YK < 0 THEN YK = 99:C = 19
170 H = (1 + ((26 * (MK + 1)) \ 10) + YK + (YK \ 4) + (5 * C + (C \ 4))) MOD 7
180 IF H = 0 THEN H = 6 ELSE H = H - 1
190 IF M = 4 OR M = 6 OR M = 9 OR M = 11 THEN D = 30:GOTO 210 ELSE IF M <> 2 THEN 210
200 D = 28 + (1 \ (Y MOD 4 + 1)) * (1 - 1 \ (Y MOD 100 + 1)) + (1 \ (Y MOD 400 + 1))
210 LOCATE 5,0:PRINT STR$(2000+Y);CHR$(1);CHR$(&H48);STR$(M);CHR$(1);CHR$(&H41)
220 LOCATE 1,1:PRINT CHR$(1);CHR$(&H47)
230 FOR I = 0 TO 5:LOCATE I*3+4,1:PRINT CHR$(1);CHR$(&H41+I):NEXT I
240 SX = H * 3:SY = 2
250 FOR I = 1 TO D
260 LOCATE SX, SY:PRINT RIGHT$(" " + STR$(I), 2)
270 IF (I + H) MOD 7 = 0 THEN SX = 0:SY = SY + 1 ELSE SX = SX + 3
280 NEXT I
